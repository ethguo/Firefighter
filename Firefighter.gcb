#chip 16f887, 8

#config MCLRE = ON

#define FULL_SPEED 200
#define DURATION_PER_DEG 6 'ms

#define DRIVE_FWD 0b10101010
#define DRIVE_BACK 0b01010101
#define DRIVE_LEFT 0b10100101
#define DRIVE_RIGHT 0b01011010

#define PWM_FREQ 16 'kHz

'===I/O PINS===
#define LINE_IN PORTB.0
#define WALL1_IN PORTE.0
#define WALL2_IN PORTE.1
#define FLAME_IN PORTE.2

#define LED0 PORTA.0
#define LED1 PORTA.1

#define FAN PORTC.0

#define M1B PORTD.0
#define M1F PORTD.1
#define M2B PORTD.2
#define M2F PORTD.3
#define M3B PORTD.4
#define M3F PORTD.5
#define M4B PORTD.6
#define M4F PORTD.7

'===LCD CONFIG===
#define LCD_IO 4
#define LCD_NO_RW
#define LCD_RS PORTA.2
#define LCD_Enable PORTA.3
#define LCD_DB4 PORTA.4
#define LCD_DB5 PORTA.5
#define LCD_DB6 PORTA.6
#define LCD_DB7 PORTA.7

'===I/O CONFIG===
DIR PORTA OUT
DIR PORTC.0 OUT
DIR PORTC.1 OUT
DIR PORTC.2 OUT
DIR PORTD OUT

DIR LINE_IN IN
DIR WALL1_IN IN
DIR WALL2_IN IN
DIR FLAME_IN IN


DIM displayClock AS BYTE
DIM runTimer AS BIT
DIM timer AS WORD
DIM mode AS WORD
DIM modeStr AS STRING
DIM duration AS WORD

displayClock = 0
runTimer = TRUE
timer = 0
mode = @Travel1
modeStr = "Init"
duration = 0

Cls

' Startup LED sequence
REPEAT 3
  Print "."
  SET LED0 ON
  SET LED1 OFF
  WAIT 333 ms
  SET LED0 OFF
  SET LED1 ON
  WAIT 333 ms
END REPEAT

Cls

SetSpeed(FULL_SPEED)

'SET PORTC.1 ON
'SET PORTC.2 ON

DO FOREVER
  ReadWall1
  ReadWall2
  ReadFlame
  IF (displayClock & 0b00001111) = 0 THEN DisplaySensors
  IndCall mode

  displayClock++
  IF runTimer THEN timer++
  Wait 4 ms
LOOP

SUB SetSpeed12(speed)
  HPWM 1, PWM_FREQ, speed
END SUB

SUB SetSpeed34(speed)
  HPWM 2, PWM_FREQ, speed
END SUB

SUB SetSpeed(speed)
  SetSpeed12(speed)
  SetSpeed34(speed)
END SUB

SUB PivotLeft(degrees)
  SetSpeed(160)
  PORTD = 0b10010110
  duration = degrees * DURATION_PER_DEG
  WAIT duration ms
  SetSpeed(FULL_SPEED)
  PORTD = 0
END SUB

SUB PivotRight(degrees)
  SetSpeed(160)
  PORTD = 0b01101001
  duration = degrees * DURATION_PER_DEG
  WAIT duration ms
  SetSpeed(FULL_SPEED)
  PORTD = 0
END SUB

SUB ReadWall1
  adval = READAD(AN5, TRUE)
  wall1 = (6787 / (adval - 3) - 4) / 5
END SUB

SUB ReadWall2
  adval = READAD(AN6, TRUE)
  wall2 = (6787 / (adval - 3) - 4) / 5
END SUB

SUB ReadFlame
  flame = READAD(AN7, TRUE)
END SUB

SUB DisplaySensors
  LED1 = !LED1
  Cls

  Print "W1"
  PadNumber(wall1)
  Print wall1

  Locate 0,6
  Print "W2"
  PadNumber(wall2)
  Print wall2

  Locate 0,12
  Print "F"
  PadNumber(flame)
  Print flame

  Locate 1,0
  Print modeStr

  Locate 1,12
  Print duration
END SUB

SUB PadNumber(num)
  IF num < 100 THEN
    Print " "
  END IF
  IF num < 10 THEN
    Print " "
  END IF
END SUB

SUB Travel1
  modeStr = "Travel1"

  IF wall2 > 40 THEN
    runTimer = TRUE
  ELSE
    runTimer = FALSE
    timer = 0
  END IF

  IF timer > 20 & wall2 > 30 THEN
    PivotRight(20)

    mode = @Room1
    EXIT SUB
  END IF

  SetSpeed(FULL_SPEED)
  PORTD = DRIVE_BACK

  IF wall2 > 11 THEN
    SetSpeed12(96)
  END IF
  IF wall2 < 10 THEN
    SetSpeed34(96)
  END IF
END SUB

SUB Room1
  modeStr = "Room1"

  IF flame < 20 THEN
    mode = @Flame1
    EXIT SUB
  END IF

  IF wall2 < 15 THEN
    timer = 0
    runTimer = TRUE

    PivotLeft(90)

    mode = @Travel2
    EXIT SUB
  END IF

  SetSpeed(FULL_SPEED)
  PORTD = DRIVE_RIGHT
END SUB

SUB Flame1
  modeStr = "Flame1"
  FAN = ON

  SetSpeed(FULL_SPEED)
  PORTD = DRIVE_FWD

  IF wall1 > 11 THEN
    SetSpeed12(96)
  END IF
  IF wall1 < 10 THEN
    SetSpeed34(96)
  END IF
END SUB

SUB Travel2
  modeStr = "Travel2"
  PORTD = DRIVE_LEFT
END SUB
