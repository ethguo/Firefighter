#chip 16f887, 8

#config MCLRE = ON

#define PWM_FREQ 16 'kHz

'===CONFIGURATION===
#define FULL_SPEED   255
#define NORMAL_SPEED 200
#define HALF_SPEED   144
#define QUARTER_SPEED 96
#define PIVOT_SPEED  160

#define DURATION_PER_DEG 6 ' ms
#define DURATION_SCAN4 50 '*4 ms
#define FLAME_THRESHOLD 16
#define FLAME_THRESHOLD_2 5

#define DRIVE_FWD   0b10101010
#define DRIVE_BACK  0b01010101
#define DRIVE_LEFT  0b10100101
#define DRIVE_RIGHT 0b01011010
#define PIVOT_LEFT  0b10010110
#define PIVOT_RIGHT 0b01101001

'===I/O PINS===
#define LINE_IN PORTB.0
#define WALL1_IN PORTE.0
#define WALL2_IN PORTE.1
#define FLAME_IN PORTE.2

#define LED0 PORTA.0
#define LED1 PORTA.1

#define FAN PORTC.0

#define M1B PORTD.0
#define M1F PORTD.1
#define M2B PORTD.2
#define M2F PORTD.3
#define M3B PORTD.4
#define M3F PORTD.5
#define M4B PORTD.6
#define M4F PORTD.7

'===LCD CONFIG===
#define LCD_IO 4
#define LCD_NO_RW
#define LCD_RS PORTA.2
#define LCD_Enable PORTA.3
#define LCD_DB4 PORTA.4
#define LCD_DB5 PORTA.5
#define LCD_DB6 PORTA.6
#define LCD_DB7 PORTA.7

'===I/O CONFIG===
DIR PORTA OUT
DIR PORTC.0 OUT
DIR PORTC.1 OUT
DIR PORTC.2 OUT
DIR PORTD OUT

DIR LINE_IN IN
DIR WALL1_IN IN
DIR WALL2_IN IN
DIR FLAME_IN IN


DIM displayClock AS BYTE
DIM runTimer AS BIT
DIM timer AS WORD
DIM mode AS WORD
DIM modeStr AS STRING
DIM pivotDuration AS WORD
DIM isFlame4 AS BIT

displayClock = 0
runTimer = TRUE
timer = 0
mode = @Travel1
modeStr = "Init"
pivotDuration = 0
isFlame4 = FALSE

Cls

' Startup LED sequence
REPEAT 3
  Print "."
  SET LED0 ON
  SET LED1 OFF
  WAIT 333 ms
  SET LED0 OFF
  SET LED1 ON
  WAIT 333 ms
END REPEAT

Cls

SetSpeed(NORMAL_SPEED)

'SET PORTC.1 ON
'SET PORTC.2 ON

DO FOREVER
  ReadWall1
  ReadWall2
  ReadFlame
  IF (displayClock & 0b00001111) = 0 THEN DisplaySensors
  IndCall mode

  displayClock++
  IF runTimer THEN timer++
  Wait 4 ms
LOOP

SUB SetSpeed12(speed)
  HPWM 1, PWM_FREQ, speed
END SUB

SUB SetSpeed34(speed)
  HPWM 2, PWM_FREQ, speed
END SUB

SUB SetSpeed(speed)
  SetSpeed12(speed)
  SetSpeed34(speed)
END SUB

SUB PivotLeft(degrees)
  SetSpeed(PIVOT_SPEED)
  PORTD = PIVOT_LEFT
  pivotDuration = degrees * DURATION_PER_DEG
  WAIT pivotDuration ms
  SetSpeed(NORMAL_SPEED)
  PORTD = 0
END SUB

SUB PivotRight(degrees)
  SetSpeed(PIVOT_SPEED)
  PORTD = PIVOT_RIGHT
  pivotDuration = degrees * DURATION_PER_DEG
  WAIT pivotDuration ms
  SetSpeed(NORMAL_SPEED)
  PORTD = 0
END SUB

SUB ReadWall1
  adval = READAD(AN5, TRUE)
  wall1 = (6787 / (adval - 3) - 4) / 5
END SUB

SUB ReadWall2
  adval = READAD(AN6, TRUE)
  wall2 = (6787 / (adval - 3) - 4) / 5
END SUB

SUB ReadFlame
  flame = READAD(AN7, TRUE)
END SUB

SUB DisplaySensors
  LED1 = !LED1
  Cls

  Print "W1"
  PadByte(wall1)
  Print wall1

  Locate 0,6
  Print "W2"
  PadByte(wall2)
  Print wall2

  Locate 0,12
  Print "F"
  PadByte(flame)
  Print flame

  Locate 1,0
  Print modeStr

  Locate 1,11
  PadWord(timer)
  Print timer

  LED0 = isFlame4
END SUB

SUB PadByte(num)
  IF num < 100 THEN Print " "
  IF num < 10 THEN Print " "
END SUB

SUB PadWord(num)
  IF num < 10000 THEN Print " "
  IF num < 1000 THEN Print " "
  IF num < 100 THEN Print " "
  IF num < 10 THEN Print " "
END SUB

SUB Travel1
  modeStr = "Travel1"

  IF wall2 > 40 THEN
    runTimer = TRUE
  ELSE
    runTimer = FALSE
    timer = 0
  END IF

  IF timer > 8 & wall2 > 30 THEN
    runTimer = TRUE
    timer = 0

    PORTD = 0
    WAIT 50 ms

    mode = @Scan4
    EXIT SUB
  END IF

  SetSpeed(FULL_SPEED)
  PORTD = DRIVE_BACK

  IF wall2 > 10 THEN
    SetSpeed12(QUARTER_SPEED)
  END IF
  IF wall2 < 9 THEN
    SetSpeed34(QUARTER_SPEED)
  END IF
END SUB

SUB Scan4
  modeStr = "Scan4"
  SetSpeed(PIVOT_SPEED)
  PORTD = PIVOT_LEFT

  IF flame < FLAME_THRESHOLD THEN isFlame4 = TRUE

  IF timer > 40 THEN
    runTimer = TRUE
    timer = 0

    PORTD = 0
    WAIT 50 ms

    mode = @Scan4_b
  END IF
END SUB

SUB Scan4_b
  modeStr = "Scan4_b"
  SetSpeed(PIVOT_SPEED)
  PORTD = PIVOT_RIGHT

  IF flame < FLAME_THRESHOLD THEN isFlame4 = TRUE

  IF timer > 53 THEN
    runTimer = TRUE
    timer = 0

    PORTD = 0
    WAIT 50 ms

    mode = @Room1
  END IF
END SUB

SUB Room1
  modeStr = "Room1"

  SetSpeed(NORMAL_SPEED)
  PORTD = DRIVE_RIGHT

  IF timer > 50 THEN
    runTimer = FALSE

    IF wall2 < 20 THEN
      PivotLeft(75)

      mode = @Room2
    END IF

    IF flame < FLAME_THRESHOLD THEN
      WAIT 50 ms

      mode = @Flame1
    END IF
  END IF
END SUB

SUB Flame1
  modeStr = "Flame1"
  FAN = ON

  SetSpeed(NORMAL_SPEED)
  PORTD = DRIVE_FWD

  IF wall1 > 11 THEN
    SetSpeed12(QUARTER_SPEED)
  END IF
  IF wall1 < 10 THEN
    SetSpeed34(QUARTER_SPEED)
  END IF
  IF wall1 < 8 THEN
    SetSpeed34(0)
  END IF
END SUB

SUB Room2
  modeStr = "Room2"
  PORTD = DRIVE_LEFT

  IF LINE_IN = 0 THEN
    IF flame < FLAME_THRESHOLD THEN
      mode = @Flame2
    ELSE
      mode = @Travel3
    END IF
  END IF
END SUB

SUB Flame2
  modeStr = "Flame2"
  FAN = ON

  SetSpeed(NORMAL_SPEED)
  PORTD = DRIVE_FWD

  IF wall1 > 11 THEN
    SetSpeed12(0)
  END IF
  IF wall1 < 10 THEN
    SetSpeed34(QUARTER_SPEED)
  END IF
  IF wall1 < 8 THEN
    SetSpeed34(0)
  END IF
END SUB

SUB Travel3
  modeStr = "Travel3"

  SetSpeed(NORMAL_SPEED)
  PORTD = DRIVE_RIGHT

  IF wall2 < 20 THEN
    SetSpeed(NORMAL_SPEED)
    PORTD = DRIVE_FWD
    WAIT 1000 ms
    mode = @Travel3_b
  END IF
END SUB

SUB Travel3_b
  modeStr = "Travel3_b"

  SetSpeed(NORMAL_SPEED)
  PORTD = DRIVE_FWD

  IF wall1 > 40 THEN
    IF isFlame4 THEN
      SetSpeed(NORMAL_SPEED)
      PORTD = 0b01010000 ' Diagonally back right
      WAIT 200 ms
      mode = @Travel4
    ELSE
      mode = @Room3
    END IF
  END IF
END SUB

SUB Room3
  modeStr = "Room3"

  SetSpeed(NORMAL_SPEED)
  PORTD = DRIVE_LEFT

  IF wall1 < 20 THEN
    IF flame < FLAME_THRESHOLD THEN
      mode = @Flame3

    ELSE
      mode = @Travel4
    END IF
  END IF
END SUB

SUB Flame3
  modeStr = "Flame3"
  FAN = ON

  SetSpeed(HALF_SPEED)
  PORTD = DRIVE_FWD

  IF LINE_IN = 0 THEN mode = @Flame3_b
END SUB

SUB Flame3_b
  modeStr = "Flame3_b"
  FAN = ON

  IF flame < FLAME_THRESHOLD_2 THEN
    PORTD = 0
  ELSE
    SetSpeed(QUARTER_SPEED)
    PORTD = PIVOT_RIGHT
  END IF
END SUB

SUB Travel4
  modeStr = "Travel4"

  SetSpeed(FULL_SPEED)
  PORTD = DRIVE_RIGHT

  IF flame < FLAME_THRESHOLD THEN
    WAIT 275 ms

    PivotRight(85)

    mode = @Room4
  END IF
END SUB

SUB Room4
  modeStr = "Room4"
  SetSpeed(NORMAL_SPEED)
  PORTD = DRIVE_LEFT

  IF LINE_IN = 0 THEN
    mode = @Flame4
  END IF
END SUB

SUB Flame4
  modeStr = "Flame4"
  FAN = ON

  IF flame < FLAME_THRESHOLD_2 THEN
    SetSpeed(104)
    PORTD = DRIVE_FWD
  ELSE
    SetSpeed(QUARTER_SPEED)
    PORTD = PIVOT_RIGHT
  END IF
END SUB
